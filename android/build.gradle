// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "29.0.2"
        // minSdkVersion 19 required by react-native-navigation (https://wix.github.io/react-native-navigation/docs/installing)
        minSdkVersion = 19
        compileSdkVersion = 29
        targetSdkVersion = 29

        // required by react-native-navigation (https://wix.github.io/react-native-navigation/docs/installing)
        RNNKotlinVersion = "1.3.61" // Or any version above 1.3.x
        RNNKotlinStdlib = "kotlin-stdlib-jdk8"
    }
    repositories {
		google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }

        // required by react-native-navigation (https://wix.github.io/react-native-navigation/docs/installing)
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'io.fabric.tools:gradle:1.+'
        
        // required by react-native-navigation (https://wix.github.io/react-native-navigation/docs/installing)
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61" // Or whatever Kotlin version you've specified above
    }
}

allprojects {
    repositories {
        // required by react-native-navigation (https://wix.github.io/react-native-navigation/docs/installing)
        google()
        mavenLocal()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            // Required for react-native-background-geolocation
            url("${project(':react-native-background-geolocation').projectDir}/libs")
        }
        maven { url 'https://jitpack.io' }      
    }
    subprojects {
        project.configurations.all {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.android.support'
                        && !details.requested.name.contains('multidex') ) {
                    details.useVersion "28.+"
                }
                if (details.requested.group == 'com.google.android.gms'
                && !details.requested.name.contains('multidex') && !details.requested.name.contains('play-services-stats')) {
                    details.useVersion "12.+"
                }
                if (details.requested.group == 'com.google.android.gms'
                && !details.requested.name.contains('multidex') && details.requested.name.contains('play-services-stats')) {
                    details.useVersion "+"
                }
            }
        }
    }
    subprojects { subproject ->
        afterEvaluate{
            if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
                android {
                    compileSdkVersion rootProject.ext.compileSdkVersion
                    buildToolsVersion rootProject.ext.buildToolsVersion
                }
            }
        }
    }
}
